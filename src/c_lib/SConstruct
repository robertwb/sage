##
## c_lib SConstruct file
##
## Author: Joel Mohler (original file)
##         Craig Citro (minor edits)
##
##
## 07 Sep 01:
## I added a fix for trac ticket 555, which is to add the "install_lib"
## and "install_includes" below.
## This is not the most elegant approach, I think. The problem is this:
## I (= Craig Citro) don't understand how to tell scons that it should
## look at /sage/local/libcsage.dylib to see if *it* is up to date. So
## what happens is that you change libcsage in branch foo, do a sage -b bar,
## and since you haven't modified sage-bar/c_lib/libcsage.dylib, scons
## doesn't think it needs to reinstall libcsage.dylib into
## $SAGE_ROOT/local/lib. (If it's not an OSX machine, then the target
## would be libcsage.so, but the point is the same.) This is obviously
## problematic; the best solution I could come up with is just always
## copying all the libraries and headers into $SAGE_ROOT where they belong.
## Actually, having discussed this with William, we came up with a better
## plan: we have two targets, install and branch_switch. With install, it's
## what you'd get with a sage -b. With branch_switch, it rebuilds the
## libraries, and then forces the copy regardless to exactly deal with
## the problem above.
##
## I also edited a bunch of lines below so that they didn't go over 80 chars,
## because I don't like seeing wrapped lines.
import os

# Note that SCons's strong point is not the './configure' step of
# autotools.  However, for this build we know that we are in a
# SAGE local filesystem.  Once we have SAGE_LOCAL imported, we
# know where everything is.
env = Environment(ENV = os.environ)

# By default, SCons hashes the source file contents to determine
# if rebuilds are necessary. If you like the old way better,
# uncomment this to use timestamps.
#env.SourceSignatures('timestamp')


# Since the sage build of python is only a static library,
# we just suppress the undefined python symbols. I don't
# really understand the other options (-single_module and
# -flat_namespace).  I can't find the documentation.
##
## These extra link flags make OS X play nice with building a dynamic
## library. -undefined dynamic_lookup is being used instead of
## -undefined suppress -- they should do basically the same thing (
## tell the linker it's okay to ignore undefined symbols at link time),
## but there's a possibility that -undefined suppress will throw an
## error, whereas -undefined dynamic_lookup will definitely try to find
## missing symbols at runtime.
## The other two options control the way the linker creates a namespace
## for the dynamic library; check the man page for ld on a mac to see
## the details.
if env['PLATFORM']=="darwin":
    env.Append( LINKFLAGS="-single_module -flat_namespace -undefined dynamic_lookup" )


## We want the debug and optimization flags, since debug symbols are so useful, etc.

env.Append( CFLAGS="-O2 -g" )

# SCons doesn't automatically pull in system environment variables
# However, we only need SAGE_LOCAL, so that's easy.
env['SAGE_LOCAL'] = os.environ['SAGE_LOCAL']

def copy_all_includes( target, source, env ):
    path = os.environ['SAGE_LOCAL'] + "/include"
    for f in source:
        Copy( path, f )
    return None

# The SCons convenience function Split is the only strange thing
# to python programmers. It just makes a list by splitting on
# whitespace without the syntax clutter of lists of strings.
includes = ['$SAGE_LOCAL/include/', '$SAGE_LOCAL/include/python2.5/',
            '$SAGE_LOCAL/include/NTL/']
cFiles = Split( "interrupt.c  mpn_pylong.c  mpz_pylong.c  stdsage.c gmp_globals.c" )
cppFiles = Split( "ZZ_pylong.cpp  ntl_wrap.cpp" )

includes_to_install = Split( "ZZ_pylong.h  ccobject.h  interrupt.h") + \
                      Split( "mpn_pylong.h  mpz_pylong.h  ntl_wrap.h  stdsage.h gmp_globals.h" )
lib = env.SharedLibrary( "csage", cFiles + cppFiles,
                         LIBS=['ntl', 'gmp'], LIBPATH=['$SAGE_LOCAL/lib'],
                         CPPPATH=includes )

env.Install( "$SAGE_LOCAL/include", includes_to_install )
env.Install( "$SAGE_LOCAL/lib", lib )

Command( "install_includes", includes_to_install, copy_all_includes )
Command( "install_lib", lib, Copy( env['SAGE_LOCAL'] + "/lib", lib ) )

env.Alias( "install", ["$SAGE_LOCAL/include", "$SAGE_LOCAL/lib"] )

env.Alias( "branch_switch", ["$SAGE_LOCAL/include", "$SAGE_LOCAL/lib",
                             "install_includes", "install_lib"] )
